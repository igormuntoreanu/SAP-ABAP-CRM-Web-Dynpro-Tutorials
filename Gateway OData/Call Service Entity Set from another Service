Call a Service Entity Set from another Service:

  METHOD get_rec_obj_veh.
    CHECK iv_profile IS NOT INITIAL.
    DATA: lt_act_guids  TYPE crmd_object_guid_tab,
          it_filter     TYPE /iwbep/t_mgw_select_option,
          lt_activities TYPE ycl_ycrma5627_act_api_mpc=>tt_activity,
          lv_importer   TYPE ycrma3000_importer.
    DATA: it_filter_select_options TYPE /iwbep/t_mgw_select_option,
          is_paging	               TYPE /iwbep/s_mgw_paging,
          it_key_tab               TYPE /iwbep/t_mgw_name_value_pair,
          it_navigation_path       TYPE /iwbep/t_mgw_navigation_path,
          it_order                 TYPE /iwbep/t_mgw_sorting_order,
          iv_filter_string         TYPE string,
          iv_search_string         TYPE string,
          io_tech_request_context	 TYPE REF TO /iwbep/if_mgw_req_entityset,
          lt_entityset_bprel       TYPE ycl_ycrma5627_veh_mpc=>tt_vehicledetails,
          es_response_context	     TYPE /iwbep/if_mgw_appl_srv_runtime=>ty_s_mgw_response_context,
          lr_entityset_bp_rel      TYPE REF TO data.


    lv_importer = get_user_importer(  ).

    "through error if importer is missing
    IF lv_importer IS INITIAL.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
        EXPORTING
          http_status_code = 500
          textid           = VALUE #(
                     msgid = yif_crma5627_api~gc_msg_class_recent_items
                     msgno = 015 "Importer couldn't be determined for user &1 !
                     attr1 = sy-uname
                     ).

    ENDIF.

    GET TIME STAMP FIELD DATA(lv_timestamp).


    TRY.

        SELECT * FROM ycrma5627_cds_c_recentobj_veh( p_importer = @lv_importer , p_timestamp = @lv_timestamp , p_profile = @iv_profile )
        INTO TABLE @DATA(lt_recent_items).

      CATCH cx_sy_dynamic_osql_semantics INTO DATA(lx_sql).

        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
          EXPORTING
            http_status_code = 500
            textid           = VALUE #(
                       msgid = yif_crma5627_api~gc_msg_class_recent_items
                       msgno = 014 "SQL Error! Contact Developer with call URL.!
                       ).

    ENDTRY.

    CHECK lt_recent_items IS NOT INITIAL.
    SORT lt_recent_items BY seq_no.
    "to get BP relation from one place
    DATA(lo_bp_rel) = NEW ycl_ycrma5627_veh_dpc_ext( ).

    DELETE ADJACENT DUPLICATES FROM lt_recent_items COMPARING vin.
    LOOP AT lt_recent_items REFERENCE INTO DATA(lr_recent_items).
      APPEND INITIAL LINE TO et_entityset REFERENCE INTO DATA(lr_entity_set).
      lr_entity_set->veh_description = lr_recent_items->veh_description.
      lr_entity_set->model_year = lr_recent_items->model_year.
      lr_entity_set->vin = lr_recent_items->vin.
      lr_entity_set->veh_guid = lr_recent_items->veh_guid.
      lr_entity_set->profile = iv_profile.
      lr_entity_set->usern_name  = sy-uname.

      it_key_tab = VALUE #( ( name = 'Vin' value = lr_recent_items->vin  ) ).
      IF it_key_tab IS NOT INITIAL.
        TRY.
            lo_bp_rel->/iwbep/if_mgw_appl_srv_runtime~get_entityset(

              EXPORTING
                iv_entity_name           = 'VehicleDetails'
                iv_entity_set_name       = 'VehicleDetailsSet'
                iv_source_name           = 'VehicleDetails'
                it_filter_select_options = it_filter_select_options
                it_order                 = it_order
                is_paging                = is_paging
                it_navigation_path       = it_navigation_path
                it_key_tab               = it_key_tab
                iv_filter_string         = iv_filter_string
                iv_search_string         = iv_search_string
                io_tech_request_context  = go_if_mgw_req_entityset
              IMPORTING
                er_entityset             = lr_entityset_bp_rel
                es_response_context      = es_response_context
            ).
          CATCH /iwbep/cx_mgw_busi_exception.
          CATCH /iwbep/cx_mgw_tech_exception.
        ENDTRY.
        FIELD-SYMBOLS: <ls_data> TYPE any.

        IF lr_entityset_bp_rel IS NOT INITIAL.
          ASSIGN lr_entityset_bp_rel->* TO <ls_data>.
          lt_entityset_bprel = <ls_data>.
          READ TABLE lt_entityset_bprel ASSIGNING FIELD-SYMBOL(<fs_bprel>) INDEX 1.

          lr_entity_set->owner = <fs_bprel>-curr_partner.
          lr_entity_set->owner_fullname = |{ <fs_bprel>-bp_name_first } { <fs_bprel>-bp_name_last  }|.
*          FIND REGEX '(?=.*At)(?=.*Dealer)' IN  lr_entity_set->owner_fullname.
          IF  <fs_bprel>-per_navig_owner = abap_true.
            lr_entity_set->owner_clickable = abap_true.
          ENDIF.
          lr_entity_set->service_customer = <fs_bprel>-service_customer.
          lr_entity_set->service_cus_fullname = |{ <fs_bprel>-service_customer_fn } { <fs_bprel>-service_customer_ln  }|.
*          FIND REGEX '(?=.*At)(?=.*Dealer)' IN  lr_entity_set->service_cus_fullname.
          IF  <fs_bprel>-per_navig_serv = abap_true.
            lr_entity_set->sercust_clickable = abap_true.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.

_________________________________________________________________________________________

METHOD dynamictileset_get_entity.

  TYPES:
    BEGIN OF ty_shuttlingloading,
      headerguid             TYPE /deereag/shut_header_guid,
      ivonlyerrors           TYPE flag,
      shuttlelisttext        TYPE /deereag/coding_text,
      shuttlelist            TYPE /deereag/shut_shuttlelist,
      trailerid              TYPE /deereag/shut_trailerid,
      statusprofile          TYPE /deereag/shut_statusprofile,
      status                 TYPE /deereag/shut_status,
      refdoctype             TYPE /deereag/shut_refdoctype,
      refdocnumber           TYPE /deereag/shut_refdocnumber,
      doorloading            TYPE /deereag/shut_doorloading,
      serviceagent           TYPE lifnr,
      creationtstamp         TYPE /deereag/shut_creation_tstamp,
      creationuser           TYPE /deereag/shut_creation_user,
      creationtcode          TYPE /deereag/shut_creation_tcode,
      changetstamp           TYPE /deereag/shut_change_tstamp,
      changeuser             TYPE /deereag/shut_change_user,
      changetcode            TYPE /deereag/shut_change_tcode,
      fastatus               TYPE /deereag/shut_fa_statusicon,
      message                TYPE bapi_msg,
      deststatus             TYPE /deereag/shut_deststatus,
      creationtstampdate     TYPE datum,
      creationtstamptime     TYPE uzeit,
      changetstampdate       TYPE datum,
      changetstamptime       TYPE uzeit,
      headerparaprofile      TYPE /deereag/shut_paraprofile,
      defaultitemparaprofile TYPE /deereag/shut_paraprofile,
      unitgw                 TYPE gewei,
      gsgew                  TYPE gsgew,
      items                  TYPE /deereag/count,
      material               TYPE matnr,
      plant                  TYPE werks_d,
      message_crit           TYPE inten,
    END OF ty_shuttlingloading .

  DATA: lt_header_entityset TYPE TABLE OF ty_shuttlingloading.

  DATA(lv_key) = VALUE #( it_key_tab[ 1 ]-value OPTIONAL ).

*  ASSIGN me->mr_request_details->* TO FIELD-SYMBOL(<fs_request_details>).

  CASE lv_key.
    WHEN '1'. "Loading
      "/sap/opu/odata/DEEREAG/GLOBAL_SHUTTLING_MONI_SRV/ShuttlingLoadingSet/$count?$filter=Status eq 'LOADING' or Status eq 'CREATED'
      DATA(lv_uri) = |/sap/opu/odata/DEEREAG/GLOBAL_SHUTTLING_MONI_SRV/ShuttlingLoadingSet?$filter=Status eq 'LOADING' or Status eq 'CREATED'|.

      er_entity-key = '1'.
      er_entity-icon = 'sap-icon://BusinessSuiteInAppSymbols/icon-truck-load'.
      er_entity-title = 'Loading'.
      er_entity-subtitle = 'Truck/Trailer'.
      er_entity-numberstate = 'Success'.
      er_entity-infostate = 'Error'.
      er_entity-numberunit = ''.
      er_entity-numberfactor = ''.
      er_entity-statearrow = ''.
    WHEN '2'. "In Transit
      "/sap/opu/odata/DEEREAG/GLOBAL_SHUTTLING_MONI_SRV/ShuttlingLoadingSet/$count?$filter=Status eq 'TRANSIT'
      lv_uri = |/sap/opu/odata/DEEREAG/GLOBAL_SHUTTLING_MONI_SRV/ShuttlingLoadingSet?$filter=Status eq 'TRANSIT'|.

      er_entity-key = '2'.
      er_entity-icon = 'sap-icon://BusinessSuiteInAppSymbols/icon-outbound-delivery'.
      er_entity-title = 'In Transit'.
      er_entity-subtitle = 'Truck/Trailer'.
      er_entity-numberstate = 'Success'.
      er_entity-infostate = 'Error'.
      er_entity-numberunit = ''.
      er_entity-numberfactor = ''.
      er_entity-statearrow = ''.

    WHEN '3'. "Unloading
      "/sap/opu/odata/DEEREAG/GLOBAL_SHUTTLING_MONI_SRV/ShuttlingLoadingSet/$count?$filter=Status eq 'UNLOADING'
      lv_uri = |/sap/opu/odata/DEEREAG/GLOBAL_SHUTTLING_MONI_SRV/ShuttlingLoadingSet?$filter=Status eq 'UNLOADING'|.

      er_entity-key = '3'.
      er_entity-icon = 'sap-icon://BusinessSuiteInAppSymbols/icon-truck-unload'.
      er_entity-title = 'Unloading'.
      er_entity-subtitle = 'Truck/Trailer'.
      er_entity-numberstate = 'Success'.
      er_entity-infostate = 'Error'.
      er_entity-numberunit = ''.
      er_entity-numberfactor = ''.
      er_entity-statearrow = ''.
    WHEN OTHERS.
  ENDCASE.

  /iwfnd/cl_sutil_client_proxy=>get_instance( )->web_request( EXPORTING
                                                                it_request_header = VALUE /iwfnd/sutil_property_t( ( name  = if_http_header_fields_sap=>request_method
                                                                value = if_http_entity=>co_request_method_get ) (
                                                                name  = if_http_header_fields_sap=>request_uri
                                                                value = lv_uri ) )
                                                              IMPORTING
                                                                ev_status_code        = DATA(lv_status_code)
                                                                ev_response_body      = DATA(lv_response_body)
                                                                ev_error_text         = DATA(lv_error_text) ).

  /iwfnd/cl_sutil_odata_mapper=>get_instance( )->convert_to_abap( EXPORTING
                                                                    iv_xdoc       = lv_response_body
                                                                  IMPORTING
                                                                    ed_data       = lt_header_entityset
                                                                    ev_error_text = lv_error_text ).
  DATA(lt_header_entityset_errors) = lt_header_entityset[].
  DELETE lt_header_entityset_errors WHERE message_crit NE '1'.

  er_entity-number = lines( lt_header_entityset ).
  er_entity-info = | { lines( lt_header_entityset_errors ) } Errors|.

ENDMETHOD.
