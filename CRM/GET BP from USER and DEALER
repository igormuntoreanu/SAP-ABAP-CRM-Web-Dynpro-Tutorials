Get BP from User

METHOD GET_BP_FROM_USER .

  DATA:   LV_USER TYPE SY-UNAME.
  DATA:   LT_BUSINESS_PARTNER TYPE STANDARD TABLE OF CRM_LRP_BP,
          LS_BUSINESS_PARTNER TYPE CRM_LRP_BP,
          LV_GUID             TYPE BU_PARTNER_GUID,
          LV_PARTNER          TYPE  BU_PARTNER.

  REFRESH: LT_BUSINESS_PARTNER.
  CLEAR:   LS_BUSINESS_PARTNER,
           LV_GUID,
           LV_PARTNER,
           EV_BP.


  IF IV_USER IS INITIAL.
    LV_USER = SY-UNAME.
  ELSE.
    LV_USER = IV_USER.
  ENDIF.


  CALL FUNCTION 'CRM_LRP_GET_BP_FOR_USER'
    EXPORTING
      USER_NAME             = LV_USER
      BEGIN_DATE            = SY-DATUM
      END_DATE              = SY-DATUM
    TABLES
      BUSINESS_PARTNER      = LT_BUSINESS_PARTNER
    EXCEPTIONS
      USER_NOT_FOUND        = 1
      WEGID_NOT_FOUND       = 2
      NO_ACTIVE_PLANVERSION = 3
      NO_RESOURCES_FOUND    = 4
      OTHERS                = 5.

  IF SY-SUBRC = 0.

    READ TABLE LT_BUSINESS_PARTNER INDEX 1 INTO LS_BUSINESS_PARTNER.
    IF SY-SUBRC = 0.
      LV_GUID  = LS_BUSINESS_PARTNER-ID.
      CALL FUNCTION 'BUPA_NUMBERS_GET'
        EXPORTING
          IV_PARTNER_GUID = LV_GUID
        IMPORTING
          EV_PARTNER      = LV_PARTNER.

      EV_BP =   LV_PARTNER.

    ENDIF.
  ENDIF.


ENDMETHOD.

___________________________________________________________________

Get Dealer From Employee

FUNCTION YDLR_PORTAL_GET_DLR_FROM_EMPL.
*"----------------------------------------------------------------------
*"*"Local interface:
*"  IMPORTING
*"     REFERENCE(IV_EMPLOYEE) TYPE  BU_PARTNER
*"  EXPORTING
*"     REFERENCE(EV_DEALER_ID) TYPE  BU_PARTNER
*"     REFERENCE(EV_DEALER_GUID) TYPE  BU_PARTNER_GUID
*"----------------------------------------------------------------------


  TYPES: BEGIN OF TY_RELATIONS,
              SIGN(1)   TYPE C,
              OPTION(2) TYPE C,
              LOW       TYPE BU_RELTYP,
              HIGH      TYPE BU_RELTYP,
            END OF TY_RELATIONS,

            BEGIN OF TY_PARTNER,
              SIGN(1)   TYPE C,
              OPTION(2) TYPE C,
              LOW       TYPE BU_PARTNER,
              HIGH      TYPE BU_PARTNER,
            END OF TY_PARTNER,

            BEGIN OF TY_ACTIONS,
              SIGN(1)   TYPE C,
              OPTION(2) TYPE C,
              LOW       TYPE BU_ACTION,
              HIGH      TYPE BU_ACTION,
            END OF TY_ACTIONS,

            BEGIN OF TY_RELTYP,
              RELTYP TYPE BU_RELTYP,
            END OF TY_RELTYP.


  DATA: LT_RELATIONS TYPE TABLE OF TY_RELATIONS,
        LS_RELATIONS LIKE LINE OF LT_RELATIONS,
        LT_PARTNER TYPE TABLE OF TY_PARTNER,
        LS_PARTNER LIKE LINE OF LT_PARTNER,
        LT_ACTION TYPE TABLE OF TY_ACTIONS,
        LS_ACTION LIKE LINE OF LT_ACTION.

  DATA: LT_RELATIONSHIPS TYPE TABLE OF BUT050,
        LS_RELATIONSHIPS LIKE LINE OF LT_RELATIONSHIPS.

  DATA: LV_DEALER_GUID TYPE BU_PARTNER_GUID.

  REFRESH: LT_RELATIONS,
           LT_PARTNER,
           LT_ACTION,
           LT_RELATIONSHIPS.

  CLEAR:   EV_DEALER_ID,
           EV_DEALER_GUID.

  CLEAR LS_RELATIONS.
  LS_RELATIONS-OPTION = 'EQ'.
  LS_RELATIONS-SIGN = 'I'.
  LS_RELATIONS-LOW = 'BUR001'.
  APPEND LS_RELATIONS TO LT_RELATIONS.

  CLEAR LS_PARTNER.
  LS_PARTNER-LOW    = IV_EMPLOYEE.
  LS_PARTNER-SIGN   = 'I'.
  LS_PARTNER-OPTION = 'EQ'.
  APPEND LS_PARTNER TO LT_PARTNER.

  CLEAR LS_ACTION.
  LS_ACTION-LOW  = 'U'.
  LS_ACTION-SIGN = 'I'.
  LS_ACTION-OPTION = 'EQ'.
  APPEND LS_ACTION TO LT_ACTION.
  LS_ACTION-LOW = 'I'.
  APPEND LS_ACTION TO LT_ACTION.

  CALL FUNCTION 'BUB_BUPR_BUT050_LM_READ'
    EXPORTING
      I_XLM2      = 'X'
      I_XDB       = 'X'
      I_XRF       = ' '
      I_XALL      = 'X'
    TABLES
      T_RELTYP    = LT_RELATIONS
      T_PARTNER1  = LT_PARTNER
      T_ACTION    = LT_ACTION
      T_RELATIONS = LT_RELATIONSHIPS.

  LOOP AT LT_RELATIONSHIPS INTO LS_RELATIONSHIPS WHERE
   DATE_FROM LE SY-DATUM AND DATE_TO GE SY-DATUM.
    CLEAR LV_DEALER_GUID.
*    check dealer
    SELECT SINGLE PARTNER_GUID
    FROM BUT000
    INTO LV_DEALER_GUID
    WHERE PARTNER = LS_RELATIONSHIPS-PARTNER1
    AND BPKIND = GC_BU_BPKIND-DEALER.
    IF SY-SUBRC = 0.
      EV_DEALER_ID = LS_RELATIONSHIPS-PARTNER1.
      EV_DEALER_GUID = LV_DEALER_GUID.
      RETURN.
    ENDIF.
  ENDLOOP.

ENDFUNCTION.
